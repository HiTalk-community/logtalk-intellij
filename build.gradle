/*
This build is configured according to the following instructions:
- https://github.com/JetBrains/gradle-intellij-plugin/blob/master/README.md
- http://www.jetbrains.org/intellij/sdk/docs/tutorials/build_system.html
- https://github.com/JetBrains/gradle-intellij-plugin
- https://docs.gradle.org/current/userguide/idea_plugin.html
*/

plugins {
    id "org.jetbrains.intellij" version "0.4.14"
    id "idea"
    id "java"
    id "groovy"
}

repositories {
    mavenCentral()
    maven {url 'https://jitpack.io'}
}


intellij {
    pluginName 'Logtalk'
    //IntelliJ dependency; for a full list of IntelliJ IDEA releases see https://www.jetbrains.com/intellij-repository/releases
    version '2019.3'
    //type = 'IC'
}

// see http://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
patchPluginXml {
    //sinceBuild '183.*'
    untilBuild '' //this is needed in order to avoid an upper-bound version.
}

group 'org.jetbrains'
version '1.0.15' // Plugin version

allprojects {
    sourceSets {
        main {
            java.srcDirs 'src/main/java', 'src/main/gen'
            resources.srcDirs 'src/main/resources'
        }
        test {
            groovy.srcDirs 'src/test/groovy'
        }
    }
}

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: '22.0'

    //todo: groovy 2.5.x does not seem to be compatible with com.jetbrains:ideaIC:2019.2
    //implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.8'

    //if this is not set to testImplementation the verifyPlugin task will fail since more than one plugin descriptor are in the classpath
    testImplementation 'com.github.JetBrains:Grammar-Kit:2019.3'

    //todo: change to '1.3-groovy-2.5' once groovy >= 2.5.x can be enabled
    testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

}
